package TimerUtils
// 
//	Credits: 
//		* Vexorian (original TimerUtils + TimedLoop)
//		* Nestharus (orignial Constant Timer Loop 32)
	
	// basic TimerUtils functions with Timer-Pool and setData/getData functions
		
	timer array freeTimers
	integer freeTimersCount = 0
	hashtable ht = InitHashtable()
	
	public function getTimer() returns timer
		if freeTimersCount > 0
			freeTimersCount -= 1
			return freeTimers[freeTimersCount]
		else
			return CreateTimer()
	
	public function timer.setData(integer data)
		SaveInteger(ht, 0, GetHandleId(this), data)
		
	
	public function timer.getData() returns integer
		return LoadInteger(ht, 0, GetHandleId(this))

	
	public function timer.release()
		this.pause()
		freeTimers[freeTimersCount] = this
		freeTimersCount += 1
	
	public function timer.pause()
		PauseTimer(this)
	
	public function timer.start(real time, code timerCallBack)
		TimerStart(this, time, false, timerCallBack)
	
	public function timer.startPeriodic(real time, code timerCallBack)
		TimerStart(this, time, true, timerCallBack)
	
	// module to execute a function after a certain time
	public module Timed
		abstract function onTimer()
		
		private static function timerCallback()
			timer t = GetExpiredTimer()
			thistype c = t.getData() castTo thistype
			c.onTimer()
			t.release()
		
		function startTimer(real time)
			timer t = getTimer()
			t.setData(this castTo integer)
			TimerStart(t, time, false, function timerCallback)
	
	// module to execute a function periodically
	public module Periodic
	
		private timer t
	
		abstract function periodic()
		
		function stopPeriodic()
			t.release()
			t = null
		
		private static function timerCallback()
			timer t = GetExpiredTimer()
			thistype c = t.getData() castTo thistype
			c.periodic()
		
		function startPeriodic(real time)
			t = getTimer()
			t.setData(this castTo integer)
			TimerStart(t, time, true, function timerCallback)
	
	
	// TimedLoop
	// originally created by Vexorian
	// augmented by some ideas from "Constant Timer Loop 32" by Nestharus
	
	
	timer timedLoopTimer = CreateTimer()
	trigger timedLoopTrig = CreateTrigger()
	int conditionCount
	
	function evaluateTimedLoopTrig()
		BJDebugMsg("timedLoop - evaluate")
		TriggerEvaluate(timedLoopTrig)
		if conditionCount == 0
			timedLoopTimer.pause()
	
	
	public module TimedLoop
		constant static real PERIOD = 0.025
		static private thistype array instances		// The array
		static private integer instanceCount = 0	// The count
		static private triggercondition triggerCond = null // the triggerCondition for this
		static private conditionfunc cond = Condition(function onExpire)
	
		// TODO this should be private but does not work currently
		protected boolean stopped = false
	
		abstract function onTimedLoop()
	
		private static function onExpire() returns boolean
			int i = instanceCount - 1
			while i >= 0
				thistype t = instances[i]
				t.onTimedLoop()
				if (t.stopped)
					instanceCount--
					instances[i] = instances[instanceCount]
					destroy t
				i -= 1
			if instanceCount == 0
				TriggerRemoveCondition(timedLoopTrig, triggerCond)
				triggerCond = null
				conditionCount--
			return false
	
		function stopTimedLoop()
			stopped = true
	
		function startTimedLoop()
			BJDebugMsg("timedLoop - 1")
			instances[instanceCount] = this
			instanceCount++
			BJDebugMsg("timedLoop - 2")
			if instanceCount == 1
				BJDebugMsg("timedLoop - 3")
				if triggerCond == null
					BJDebugMsg("timedLoop - 4")
					triggerCond = TriggerAddCondition(timedLoopTrig, cond)
					conditionCount++
					BJDebugMsg("timedLoop - 5")
					if conditionCount == 1
						BJDebugMsg("timedLoop - 6")
						timedLoopTimer.startPeriodic(PERIOD, function evaluateTimedLoopTrig)	 
						BJDebugMsg("timedLoop - 7")

endpackage


