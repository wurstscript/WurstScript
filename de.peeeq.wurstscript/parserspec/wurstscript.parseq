package de.peeeq.wurstscript.ast


abstract syntax:

Identifier(de.peeeq.wurstscript.parser.WPos source, String name)

WurstModel * CompilationUnit

CompilationUnit( String file, de.peeeq.wurstscript.attributes.ErrorHandler cuErrorHandler, 
	JassToplevelDeclarations jassDecls, WPackages packages)


JassToplevelDeclarations * JassToplevelDeclaration

WPackages * WPackage 


TopLevelDeclaration = JassToplevelDeclaration | WPackage
TopLevelDeclarations * TopLevelDeclaration

WPackage(de.peeeq.wurstscript.parser.WPos source, Modifiers modifiers, Identifier nameId, WImports imports, WEntities elements)

WImports * WImport
WEntities * WEntity

WImport(de.peeeq.wurstscript.parser.WPos source, boolean isPublic, boolean isInitLater, Identifier packagenameId)

WEntity =
	  TypeDef
	| FuncDef
	| GlobalVarDef
	| ExtensionFuncDef(de.peeeq.wurstscript.parser.WPos source, Modifiers modifiers, TypeExpr extendedType, Identifier nameId, TypeParamDefs typeParameters, WParameters parameters, OptTypeExpr returnTyp, WStatements body)
	| InitBlock (de.peeeq.wurstscript.parser.WPos source, WStatements body)
	| NativeFunc(de.peeeq.wurstscript.parser.WPos source, Modifiers modifiers, Identifier nameId, WParameters parameters, OptTypeExpr returnTyp)
	| ModuleDef(de.peeeq.wurstscript.parser.WPos source, Modifiers modifiers, Identifier nameId, TypeParamDefs typeParameters,
			ClassDefs innerClasses, FuncDefs methods, GlobalVarDefs vars, ConstructorDefs constructors,
			ModuleInstanciations p_moduleInstanciations, ModuleUses moduleUses, OnDestroyDef onDestroy)



TypeExpr =
	  TypeExprSimple(de.peeeq.wurstscript.parser.WPos source, OptTypeExpr scopeType, String typeName, TypeExprList typeArgs)
	| TypeExprArray(de.peeeq.wurstscript.parser.WPos source, TypeExpr base, OptExpr arraySize)
	| TypeExprThis(de.peeeq.wurstscript.parser.WPos source, OptTypeExpr scopeType)
	| TypeExprResolved(de.peeeq.wurstscript.parser.WPos source, de.peeeq.wurstscript.types.WurstType resolvedType)


OptTypeExpr = NoTypeExpr() | TypeExpr
OptExpr = NoExpr() | Expr

TypeDef =
	  NativeType(de.peeeq.wurstscript.parser.WPos source, Modifiers modifiers, Identifier nameId, OptTypeExpr optTyp)
	| ClassDef(de.peeeq.wurstscript.parser.WPos source, Modifiers modifiers, Identifier nameId, TypeParamDefs typeParameters, OptTypeExpr extendedClass, TypeExprList implementsList,
			ClassDefs innerClasses, FuncDefs methods, GlobalVarDefs vars, ConstructorDefs constructors,
			ModuleInstanciations p_moduleInstanciations, ModuleUses moduleUses, OnDestroyDef onDestroy)
	| InterfaceDef(de.peeeq.wurstscript.parser.WPos source, Modifiers modifiers, Identifier nameId, TypeParamDefs typeParameters, TypeExprList extendsList,
			FuncDefs methods, GlobalVarDefs vars, ConstructorDefs constructors,
			ModuleInstanciations moduleInstanciations, ModuleUses moduleUses, OnDestroyDef onDestroy)
	| TupleDef(de.peeeq.wurstscript.parser.WPos source, Modifiers modifiers, Identifier nameId, WParameters parameters, OptTypeExpr returnTyp)
	| TypeParamDef
	| EnumDef(de.peeeq.wurstscript.parser.WPos source, Modifiers modifiers, Identifier nameId, EnumMembers members )
	| StructureDef

ClassDefs * ClassDef
EnumMembers * EnumMember
FuncDefs * FuncDef
GlobalVarDefs * GlobalVarDef
ConstructorDefs * ConstructorDef
ModuleInstanciations * ModuleInstanciation
ModuleUses * ModuleUse
TypeExprList * TypeExpr
SwitchCases * SwitchCase


SwitchCase( de.peeeq.wurstscript.parser.WPos source, Expr expr, WStatements stmts )
SwitchDefaultCase = SwitchDefaultCaseStatements(de.peeeq.wurstscript.parser.WPos source, WStatements stmts) | NoDefaultCase()

ClassSlot =
	  ConstructorDef(de.peeeq.wurstscript.parser.WPos source, Modifiers modifiers, WParameters parameters, boolean isExplicit, Arguments superArgs, WStatements body)
	| OnDestroyDef(de.peeeq.wurstscript.parser.WPos source, WStatements body)
	| ModuleUse(de.peeeq.wurstscript.parser.WPos source, Identifier moduleNameId, TypeExprList typeArgs)
	| ModuleInstanciation(de.peeeq.wurstscript.parser.WPos source, Modifiers modifiers, Identifier nameId,
			ClassDefs innerClasses, FuncDefs methods, GlobalVarDefs vars, ConstructorDefs constructors,
			ModuleInstanciations p_moduleInstanciations, ModuleUses moduleUses, OnDestroyDef onDestroy)
	| ClassMember
	| ClassDef

ClassMember =
	  GlobalVarDef
	| FuncDef

EnumMember(de.peeeq.wurstscript.parser.WPos source, Modifiers modifiers, Identifier nameId)


GlobalVarDef(de.peeeq.wurstscript.parser.WPos source, Modifiers modifiers, OptTypeExpr optTyp, Identifier nameId, OptExpr initialExpr)
LocalVarDef(de.peeeq.wurstscript.parser.WPos source, Modifiers modifiers, OptTypeExpr optTyp, Identifier nameId, OptExpr initialExpr)


FuncDef(de.peeeq.wurstscript.parser.WPos source, Modifiers modifiers, Identifier nameId, TypeParamDefs typeParameters, WParameters parameters, OptTypeExpr returnTyp, WStatements body)



ArraySizes * Expr

//FuncSignature(de.peeeq.wurstscript.parser.WPos source, Identifier nameId, TypeParamDefs typeParameters, WParameters parameters, OptTypeExpr returnTyp)

TypeParamDefs * TypeParamDef

TypeParamDef(de.peeeq.wurstscript.parser.WPos source, Modifiers modifiers, Identifier nameId)


WParameters * WParameter
WParameter(de.peeeq.wurstscript.parser.WPos source, Modifiers modifiers, TypeExpr typ, Identifier nameId)


WStatements * WStatement

ExprOrStatements = WStatements | WStatement | Expr

WStatement = ActionStatement | ControlflowStatement | EndFunctionStatement(de.peeeq.wurstscript.parser.WPos source) | StartFunctionStatement(de.peeeq.wurstscript.parser.WPos source)

ActionStatement =
	  StmtSkip(de.peeeq.wurstscript.parser.WPos source)
	| LocalVarDef
	| StmtSet(de.peeeq.wurstscript.parser.WPos source, NameRef updatedExpr, Expr right)
	| StmtCall
	| StmtErr(de.peeeq.wurstscript.parser.WPos source)
	| ExprMemberVar
	| ExprVarAccess
	| ExprDestroy


ControlflowStatement =
	  CompoundStatement
	| StmtReturn(de.peeeq.wurstscript.parser.WPos source, OptExpr returnedObj)
	| StmtExitwhen(de.peeeq.wurstscript.parser.WPos source, Expr cond)

CompoundStatement =
	  StmtIf(de.peeeq.wurstscript.parser.WPos source, Expr cond, WStatements thenBlock, WStatements elseBlock)
	| SwitchStmt(de.peeeq.wurstscript.parser.WPos source, Expr expr, SwitchCases cases, SwitchDefaultCase switchDefault)
	| WBlock(de.peeeq.wurstscript.parser.WPos source, WStatements body)
	| LoopStatement

LoopStatement =
	  StmtWhile(de.peeeq.wurstscript.parser.WPos source, Expr cond, WStatements body)
	| StmtLoop(de.peeeq.wurstscript.parser.WPos source, WStatements body)
	| LoopStatementWithVarDef

LoopStatementWithVarDef =
	  StmtForRange
	| StmtForIn(de.peeeq.wurstscript.parser.WPos source, LocalVarDef loopVar, Expr in, WStatements body)
	| StmtForFrom(de.peeeq.wurstscript.parser.WPos source, LocalVarDef loopVar, Expr in, WStatements body)

StmtForRange =
	  StmtForRangeUp(de.peeeq.wurstscript.parser.WPos source, LocalVarDef loopVar, Expr to, Expr step, WStatements body)
	| StmtForRangeDown(de.peeeq.wurstscript.parser.WPos source, LocalVarDef loopVar, Expr to, Expr step, WStatements body)

Expr =
	  NameRef
	| FunctionCall
	| ExprBinary(de.peeeq.wurstscript.parser.WPos source, Expr left, de.peeeq.wurstscript.WurstOperator op, Expr right)
	| ExprUnary(de.peeeq.wurstscript.parser.WPos source, de.peeeq.wurstscript.WurstOperator opU, Expr right)
	| ExprMember
	| ExprFunctionCall(de.peeeq.wurstscript.parser.WPos source, Identifier funcNameId, TypeExprList typeArgs, Arguments args)
	| ExprNewObject(de.peeeq.wurstscript.parser.WPos source, Identifier typeNameId, TypeExprList typeArgs, Arguments args)
	| ExprCast(de.peeeq.wurstscript.parser.WPos source, TypeExpr typ, Expr expr)
	| ExprInstanceOf(de.peeeq.wurstscript.parser.WPos source, TypeExpr typ, Expr expr)
	| ExprClosure(de.peeeq.wurstscript.parser.WPos source, WParameters parameters, Expr implementation)
	| ExprAtomic
	| ExprIncomplete(de.peeeq.wurstscript.parser.WPos source, String errorMessage)
	| ExprStatementsBlock(de.peeeq.wurstscript.parser.WPos source, WStatements body)
	| ExprDestroy(de.peeeq.wurstscript.parser.WPos source, Expr destroyedObj)

ExprMember =
	  ExprMemberVar
	| ExprMemberArrayVar
	| ExprMemberMethod
	| ExprTypeId(de.peeeq.wurstscript.parser.WPos source, Expr left)


ExprMemberVar =
	  ExprMemberVarDot(de.peeeq.wurstscript.parser.WPos source, Expr left, Identifier varNameId)
	| ExprMemberVarDotDot(de.peeeq.wurstscript.parser.WPos source, Expr left, Identifier varNameId)

ExprMemberArrayVar =
	  ExprMemberArrayVarDot(de.peeeq.wurstscript.parser.WPos source, Expr left, Identifier varNameId, Indexes indexes)
	| ExprMemberArrayVarDotDot(de.peeeq.wurstscript.parser.WPos source, Expr left, Identifier varNameId, Indexes indexes)

ExprMemberMethod =
	  ExprMemberMethodDot(de.peeeq.wurstscript.parser.WPos source, Expr left, Identifier funcNameId, TypeExprList typeArgs, Arguments args)
	| ExprMemberMethodDotDot(de.peeeq.wurstscript.parser.WPos source, Expr left, Identifier funcNameId, TypeExprList typeArgs, Arguments args)


ExprAtomic =
	  ExprIntVal(de.peeeq.wurstscript.parser.WPos source, String valIraw)
	| ExprRealVal(de.peeeq.wurstscript.parser.WPos source, String valR)
	| ExprStringVal(de.peeeq.wurstscript.parser.WPos source, String valS)
	| ExprBoolVal(de.peeeq.wurstscript.parser.WPos source, boolean valB)
	| ExprFuncRef(de.peeeq.wurstscript.parser.WPos source, String scopeName, Identifier funcNameId)
	| ExprVarAccess(de.peeeq.wurstscript.parser.WPos source, Identifier varNameId)
	| ExprVarArrayAccess(de.peeeq.wurstscript.parser.WPos source, Identifier varNameId, Indexes indexes)
	| ExprThis(de.peeeq.wurstscript.parser.WPos source)
	| ExprNull(de.peeeq.wurstscript.parser.WPos source)
	| ExprSuper(de.peeeq.wurstscript.parser.WPos source)
	| ExprEmpty(de.peeeq.wurstscript.parser.WPos source)


Indexes * Expr
Arguments * Expr




// auxiliary types:

IdentifierWithTypeArgs(de.peeeq.wurstscript.parser.WPos source, Identifier nameId, &TypeExprList typeArgs)

IdentifierWithTypeParamDefs(de.peeeq.wurstscript.parser.WPos source, Identifier nameId, &TypeParamDefs typeParams)


// a reference to a variable, needs a varName:String attribute
NameRef = ExprVarArrayAccess | ExprVarAccess | ExprMemberVar | ExprMemberArrayVar

VarDef =
	  GlobalOrLocalVarDef
	| WParameter

GlobalOrLocalVarDef =
	  GlobalVarDef
	| LocalVarDef

// has funcName:String attribute

StmtCall = FunctionCall | ExprNewObject

FunctionCall = ExprMemberMethod | ExprFunctionCall

FuncRef = ExprFuncRef | FunctionCall


NotExtensionFunction = NativeFunc | FuncDef | TupleDef

FunctionDefinition = NotExtensionFunction | ExtensionFuncDef | FunctionImplementation

FunctionImplementation = FuncDef | ExtensionFuncDef

FunctionLike = FunctionImplementation | ConstructorDef | InitBlock | OnDestroyDef

Documentable = NameDef | ConstructorDef



WScope =
	  NamedScope
	| PackageOrGlobal
	| FunctionDefinition
	| FunctionLike
	| WurstModel
	| WStatements
	| LoopStatementWithVarDef
	| WBlock
	| WEntities
	| ExprClosure

PackageOrGlobal = WPackage | CompilationUnit

TypeRef = TypeExpr | ExprNewObject

NameDef = NamedScope | VarDef | TypeDef | ModuleDef | ModuleInstanciation | FunctionDefinition | WPackage | EnumDef | EnumMember

VisibilityModifier =
	  VisibilityPublic(de.peeeq.wurstscript.parser.WPos source)
	| VisibilityPrivate(de.peeeq.wurstscript.parser.WPos source)
	| VisibilityPublicread(de.peeeq.wurstscript.parser.WPos source)
	| VisibilityProtected(de.peeeq.wurstscript.parser.WPos source)
	| VisibilityDefault(de.peeeq.wurstscript.parser.WPos source)

Modifiers * Modifier

Modifier =
	  VisibilityModifier
	| Annotation(de.peeeq.wurstscript.parser.WPos source, String annotationType)
	| ModStatic(de.peeeq.wurstscript.parser.WPos source)
	| ModOverride(de.peeeq.wurstscript.parser.WPos source)
	| ModAbstract(de.peeeq.wurstscript.parser.WPos source)
	| ModConstant(de.peeeq.wurstscript.parser.WPos source)
	| WurstDoc(de.peeeq.wurstscript.parser.WPos source, String rawComment)

// AstElementWithBody = FunctionImplementation | InitBlock | ConstructorDef | OnDestroyDef
//AstElementWithModifier = NameDef | TypeDef | ModuleDef | ConstructorDef | GlobalVarDef | FunctionDefinition
HasModifier = NameDef | TypeDef | ModuleDef | ConstructorDef | GlobalVarDef | FunctionDefinition
HasTypeArgs =  ExprNewObject | FunctionCall | ModuleUse | StmtCall | TypeExprSimple


ClassOrModule = ClassDef | ModuleDef

StructureDef = ClassOrModuleOrModuleInstanciation | InterfaceDef

ClassOrModuleInstanciation = ClassDef | ModuleInstanciation
ClassOrModuleOrModuleInstanciation = ClassOrModule | ClassOrModuleInstanciation

NamedScope = WPackage | StructureDef | InterfaceDef | EnumDef

HasReadVariables = Expr | WStatement | WScope

// Jass types:


JassToplevelDeclaration = JassGlobalBlock | NotExtensionFunction | NativeType

JassGlobalBlock * GlobalVarDef


// special types for translation
TranslatedToImFunction = ConstructorDef | FunctionDefinition | InitBlock | OnDestroyDef | ExprClosure



attributes:

WStatement.attrNextStatements "returns all statements which can be executed after this statement"
	returns java.util.List<WStatement>
	implemented by de.peeeq.wurstscript.attributes.Flow.getNext

WStatement.attrPreviousStatements "returns all statements which could have been executed before this statement"
	returns java.util.List<WStatement>
	implemented by de.peeeq.wurstscript.attributes.Flow.getPrevious


CompoundStatement.attrAfterBodyStatements "returns all statements which can be executed after the body/thenBlock/elseBlock of this statement is executed"
	returns java.util.List<WStatement>
	implemented by de.peeeq.wurstscript.attributes.Flow.getAfterBody

WStatement.attrListIndex "returns the index this statement has in the underlying list"
	returns int
	implemented by  de.peeeq.wurstscript.attributes.Flow.getListIndex


HasReadVariables.attrReadVariables
	returns de.peeeq.immutablecollections.ImmutableList<NameDef>
	implemented by de.peeeq.wurstscript.attributes.ReadVariables.calculate


OptTypeExpr.attrTyp
	returns de.peeeq.wurstscript.types.WurstType
	implemented by de.peeeq.wurstscript.attributes.AttrTypeExprType.normalizedType

Expr.attrTyp
	returns de.peeeq.wurstscript.types.WurstType
	implemented by de.peeeq.wurstscript.attributes.AttrExprType.normalizedType


Expr.attrConstantValue
	returns de.peeeq.wurstscript.intermediateLang.ILconst
	implemented by de.peeeq.wurstscript.attributes.AttrConstantValue.calculate


OptTypeExpr.attrTypRaw
	returns de.peeeq.wurstscript.types.WurstType
	implemented by de.peeeq.wurstscript.attributes.AttrTypeExprType.calculate

Expr.attrTypRaw
	returns de.peeeq.wurstscript.types.WurstType
	implemented by de.peeeq.wurstscript.attributes.AttrExprType.calculate

Expr.attrExpectedTypRaw
	returns de.peeeq.wurstscript.types.WurstType
	implemented by de.peeeq.wurstscript.attributes.AttrExprExpectedType.calculate

Expr.attrExpectedTyp "try to guess what type is expected here"
	returns de.peeeq.wurstscript.types.WurstType
	implemented by de.peeeq.wurstscript.attributes.AttrExprExpectedType.normalizedType

ExprClosure.attrClosureAbstractMethod
	returns de.peeeq.wurstscript.attributes.names.NameLink
	implemented by de.peeeq.wurstscript.attributes.AttrClosureAbstractMethod.calculate

ExprClosure.attrCapturedVariables
	returns com.google.common.collect.ImmutableMultimap<AstElement,VarDef>
	implemented by de.peeeq.wurstscript.attributes.AttrClosureCapturedVariables.calculate


FunctionCall.attrImplicitParameter
	returns OptExpr
	implemented by de.peeeq.wurstscript.attributes.AttrImplicitParameter.getImplicitParameter

NameRef.attrImplicitParameter
	returns OptExpr
	implemented by de.peeeq.wurstscript.attributes.AttrImplicitParameter.getImplicitParameter


NameDef.attrTyp
	returns de.peeeq.wurstscript.types.WurstType
	implemented by de.peeeq.wurstscript.attributes.AttrVarDefType.calculate

FunctionLike.attrReturnType
	returns de.peeeq.wurstscript.types.WurstType
	implemented by de.peeeq.wurstscript.attributes.AttrVarDefType.calculate

VarDef.attrIsDynamicClassMember
	returns boolean
	implemented by de.peeeq.wurstscript.attributes.AttrIsClassMember.calculate

FunctionDefinition.attrIsDynamicClassMember
	returns boolean
	implemented by de.peeeq.wurstscript.attributes.AttrIsClassMember.calculate

FunctionDefinition.attrParameterTypesIncludingReceiver
	returns com.google.common.collect.ImmutableList<de.peeeq.wurstscript.types.WurstType>
	implemented by de.peeeq.wurstscript.attributes.AttrParameterTypes.parameterTypesIncludingReceiver

FunctionDefinition.attrParameterTypes
	returns com.google.common.collect.ImmutableList<de.peeeq.wurstscript.types.WurstType>
	implemented by de.peeeq.wurstscript.attributes.AttrParameterTypes.parameterTypes

FunctionDefinition.attrReceiverType
	returns de.peeeq.wurstscript.types.WurstType
	implemented by de.peeeq.wurstscript.attributes.AttrParameterTypes.receiverType

AstElement.attrIsDynamicContext
	returns boolean
	implemented by de.peeeq.wurstscript.attributes.IsDynamicContext.calculate

AstElement.attrNearestPackage
	returns @Nullable PackageOrGlobal
	implemented by de.peeeq.wurstscript.attributes.AttrNearest.nearestPackage

AstElement.attrNearestNamedScope
	returns @Nullable NamedScope
	implemented by de.peeeq.wurstscript.attributes.AttrNearest.nearestNamedScope

AstElement.attrNearestScope
	returns @Nullable WScope
	implemented by de.peeeq.wurstscript.attributes.AttrNearest.nearestScope

WScope.attrNextScope "returns the scope surrounding this scope"
	returns @Nullable WScope
	implemented by de.peeeq.wurstscript.attributes.AttrNearest.nextScope


AstElement.attrPathDescription
	returns String
	implemented by de.peeeq.wurstscript.attributes.PathDescription.get

AstElement.attrCompilationUnit
	returns CompilationUnit
	implemented by de.peeeq.wurstscript.attributes.AttrNearest.nearestCompilationUnit

AstElement.attrNearestClassDef
	returns @Nullable ClassDef
	implemented by de.peeeq.wurstscript.attributes.AttrNearest.nearestClassDef

AstElement.attrNearestClassOrModule
	returns @Nullable ClassOrModule
	implemented by de.peeeq.wurstscript.attributes.AttrNearest.nearestClassOrModule

AstElement.attrNearestStructureDef
	returns @Nullable StructureDef
	implemented by de.peeeq.wurstscript.attributes.AttrNearest.nearestStructureDef

AstElement.attrNearestFuncDef
	returns @Nullable FunctionImplementation
	implemented by de.peeeq.wurstscript.attributes.AttrNearest.nearestFuncDef


AstElement.attrNearestExprClosure
	returns @Nullable ExprClosure
	implemented by de.peeeq.wurstscript.attributes.AttrNearest.nearestExprClosure

AstElement.attrNearestExprStatementsBlock
	returns @Nullable ExprStatementsBlock
	implemented by de.peeeq.wurstscript.attributes.AttrNearest.nearestExprStatementsBlock



AstElement.tryGetNameDef() "returns the element itself if it is a NameDef or returns the referenced NameDef if the elem is a reference or returns null otherwise "
	returns @Nullable NameDef
	implemented by de.peeeq.wurstscript.attributes.AttrNameDef.tryGetNameDef


ExprStatementsBlock.getReturnStmt()
	returns StmtReturn
	implemented by de.peeeq.wurstscript.attributes.SmallHelpers.getReturnStatement



HasModifier.attrIsCompiletime
	returns boolean
	implemented by de.peeeq.wurstscript.attributes.ModifiersHelper.isCompiletime

HasModifier.attrHasAnnotation(String name)
	returns boolean
	implemented by de.peeeq.wurstscript.attributes.ModifiersHelper.hasAnnotation

HasModifier.attrIsPublic
	returns boolean
	implemented by de.peeeq.wurstscript.attributes.ModifiersHelper.isPublic

HasModifier.attrIsPublicRead
	returns boolean
	implemented by de.peeeq.wurstscript.attributes.ModifiersHelper.isPublicRead

HasModifier.attrIsPrivate
	returns boolean
	implemented by de.peeeq.wurstscript.attributes.ModifiersHelper.isPrivate

HasModifier.attrIsProtected
	returns boolean
	implemented by de.peeeq.wurstscript.attributes.ModifiersHelper.isProtected

HasModifier.attrIsStatic
	returns boolean
	implemented by de.peeeq.wurstscript.attributes.ModifiersHelper.isStatic

HasModifier.attrIsOverride
	returns boolean
	implemented by de.peeeq.wurstscript.attributes.ModifiersHelper.isOverride

HasModifier.attrIsAbstract
	returns boolean
	implemented by de.peeeq.wurstscript.attributes.ModifiersHelper.isAbstract

HasModifier.attrIsConstant
	returns boolean
	implemented by de.peeeq.wurstscript.attributes.ModifiersHelper.isConstant



ClassOrModuleOrModuleInstanciation.getModuleInstanciations
	returns ModuleInstanciations
	implemented by de.peeeq.wurstscript.ModuleExpander.expandModules

ClassOrModule.attrUsedModules
	returns com.google.common.collect.ImmutableCollection<ModuleDef>
	implemented by de.peeeq.wurstscript.attributes.AttrUsedModules.calculate

WImport.attrImportedPackage
	returns @Nullable WPackage
	implemented by de.peeeq.wurstscript.attributes.AttrImportedPackage.getImportedPackage



ModuleInstanciation.attrModuleOrigin
	returns ModuleDef
	implemented by de.peeeq.wurstscript.attributes.AttrModuleInstanciations.getModuleOrigin

StmtCall.attrPossibleFunctionSignatures
	returns com.google.common.collect.ImmutableCollection<de.peeeq.wurstscript.types.FunctionSignature>
	implemented by de.peeeq.wurstscript.attributes.AttrPossibleFunctionSignatures.calculate

StmtCall.attrFunctionSignature
	returns de.peeeq.wurstscript.types.FunctionSignature
	implemented by de.peeeq.wurstscript.attributes.AttrFunctionSignature.calculate

StmtCall.attrCallSignature
	returns de.peeeq.wurstscript.types.CallSignature
	implemented by de.peeeq.wurstscript.attributes.AttrCallSignature.calculate

AstElement.attrSource()
	returns de.peeeq.wurstscript.parser.WPos
	implemented by de.peeeq.wurstscript.attributes.AttrPos.getPos

AstElement.attrErrorPos() "returns the position where errors are marked"
	returns de.peeeq.wurstscript.parser.WPos
	implemented by de.peeeq.wurstscript.attributes.AttrPos.getErrorPos


HasTypeArgs.attrTypeParameterBindings
	returns com.google.common.collect.ImmutableMap<TypeParamDef, de.peeeq.wurstscript.types.WurstType>
	implemented by de.peeeq.wurstscript.attributes.Generics.getTypeParameterBindings





InterfaceDef.attrExtendedInterfaces
	returns com.google.common.collect.ImmutableCollection<de.peeeq.wurstscript.types.WurstTypeInterface>
	implemented by de.peeeq.wurstscript.attributes.InterfaceInstances.getExtendedInterfaces

ClassDef.attrExtendedClass
	returns ClassDef
	implemented by de.peeeq.wurstscript.attributes.SubClasses.getExtendedClass

ClassDef.attrExtendedClassType
	returns de.peeeq.wurstscript.types.WurstType
	implemented by de.peeeq.wurstscript.attributes.SubClasses.getExtendedClassType

ClassDef.isInnerClass()
	returns boolean
	implemented by de.peeeq.wurstscript.attributes.InnerClasses.isInnerClass

ConstructorDef.attrSuperConstructor
	returns ConstructorDef
	implemented by de.peeeq.wurstscript.attributes.SubClasses.getSuperConstructor



WurstModel.attrPackages
	returns com.google.common.collect.ImmutableMap<String, WPackage>
	implemented by de.peeeq.wurstscript.attributes.AttrImportedPackage.getPackages


AstElement.getModel()
	returns WurstModel
	implemented by de.peeeq.wurstscript.attributes.AttrImportedPackage.getModel

CompilationUnit.attrGetByType
	returns de.peeeq.wurstscript.attributes.ByTypes
	implemented by de.peeeq.wurstscript.attributes.GetByType.calculate


WurstModel.attrConfigOverridePackages
	returns com.google.common.collect.ImmutableMap<WPackage, WPackage>
	implemented by de.peeeq.wurstscript.attributes.CofigOverridePackages.calculate

NameDef.attrConfigActualNameDef
	returns NameDef
	implemented by de.peeeq.wurstscript.attributes.CofigActualDef.calculate


NameDef.hasAnnotation(String annotation)
	returns boolean
	implemented by de.peeeq.wurstscript.attributes.HasAnnotation.hasAnnotation



// Im translation

	WPackage.imTranslateTLD(
			de.peeeq.wurstscript.translation.imtranslation.ImTranslator translator
		)
		returns void
		implemented by de.peeeq.wurstscript.translation.imtranslation.TLDTranslation.translate

	JassToplevelDeclaration.imTranslateTLD(
			de.peeeq.wurstscript.translation.imtranslation.ImTranslator translator
		)
		returns void
		implemented by de.peeeq.wurstscript.translation.imtranslation.TLDTranslation.translate


	WEntity.imTranslateEntity(
			de.peeeq.wurstscript.translation.imtranslation.ImTranslator translator
		)
		returns void
		implemented by de.peeeq.wurstscript.translation.imtranslation.TLDTranslation.translate

	Expr.imTranslateExpr(
			de.peeeq.wurstscript.translation.imtranslation.ImTranslator translator,
			de.peeeq.wurstscript.jassIm.ImFunction f
		)
		returns de.peeeq.wurstscript.jassIm.ImExpr
		implemented by de.peeeq.wurstscript.translation.imtranslation.ExprTranslation.translate

	OptExpr.imTranslateExprOpt(
			de.peeeq.wurstscript.translation.imtranslation.ImTranslator translator,
			de.peeeq.wurstscript.jassIm.ImFunction f
		)
		returns de.peeeq.wurstscript.jassIm.ImExprOpt
		implemented by de.peeeq.wurstscript.translation.imtranslation.ExprTranslation.translate


	WStatement.imTranslateStmt(
			de.peeeq.wurstscript.translation.imtranslation.ImTranslator translator,
			de.peeeq.wurstscript.jassIm.ImFunction f
		)
		returns de.peeeq.wurstscript.jassIm.ImStmt
		implemented by de.peeeq.wurstscript.translation.imtranslation.StmtTranslation.translate


	TranslatedToImFunction.imCreateFuncSkeleton(
			de.peeeq.wurstscript.translation.imtranslation.ImTranslator translator,
			de.peeeq.wurstscript.jassIm.ImFunction f
		)
		returns void
		implemented by de.peeeq.wurstscript.translation.imtranslation.FuncSkeleton.create

	ClassDef.attrImplementedInterfaces
		returns com.google.common.collect.ImmutableCollection<de.peeeq.wurstscript.types.WurstTypeInterface>
		implemented by de.peeeq.wurstscript.attributes.InterfaceInstances.getImplementedInterfaces



AstElement.addError(String msg)
	returns void
	implemented by de.peeeq.wurstscript.attributes.ErrorHandling.addError

AstElement.addWarning(String msg)
	returns void
	implemented by de.peeeq.wurstscript.attributes.ErrorHandling.addWarning


AstElement.getErrorHandler()
	returns de.peeeq.wurstscript.attributes.ErrorHandler
	implemented by de.peeeq.wurstscript.attributes.ErrorHandling.getErrorHandler


FunctionLike.attrHasEmptyBody()
	returns boolean
	implemented by de.peeeq.wurstscript.attributes.SmallHelpers.hasEmptyBody

// Naming:

NameRef.attrNameDef
	returns @Nullable NameDef
	implemented by de.peeeq.wurstscript.attributes.AttrNameDef.calculate

FuncRef.attrPossibleFuncDefs
	returns com.google.common.collect.ImmutableCollection<FunctionDefinition>
	implemented by de.peeeq.wurstscript.attributes.PossibleFuncDefs.calculate


FuncRef.attrFuncDef
	returns @Nullable FunctionDefinition
	implemented by de.peeeq.wurstscript.attributes.AttrFuncDef.calculate

ExprNewObject.attrConstructorDef
	returns @Nullable ConstructorDef
	implemented by de.peeeq.wurstscript.attributes.AttrConstructorDef.calculate

ExprBinary.attrFuncDef
	returns @Nullable FunctionDefinition
	implemented by de.peeeq.wurstscript.attributes.AttrFuncDef.calculate


FunctionDefinition.attrRealFuncDef "returns the function definition which overrides this definition"
	returns @Nullable FunctionDefinition
	implemented by de.peeeq.wurstscript.attributes.OverriddenFunctions.getRealFuncDef

StructureDef.attrLevel "returns the level in the hierarchy. no supertype -> level 1"
	returns int
	implemented by de.peeeq.wurstscript.attributes.Level.get

NameDef.createNameLink(WScope definedIn) "returns a namelink pointing to this NameDef"
	returns de.peeeq.wurstscript.attributes.names.NameLink
	implemented by de.peeeq.wurstscript.attributes.names.NameLink.create

ModuleUse.attrModuleDef
	returns @Nullable ModuleDef
	implemented by de.peeeq.wurstscript.attributes.AttrModuleDef.calculate

TypeRef.attrTypeDef
	returns @Nullable TypeDef
	implemented by de.peeeq.wurstscript.attributes.AttrTypeDef.calculate


//WPackage.attrExportedNames "get the exported names of a package"
//	returns  com.google.common.collect.ImmutableMultimap<String, NameDef>
//	implemented by de.peeeq.wurstscript.attributes.Scopes.getExportedNames

//WScope.attrDefinedNames "get the names defined in this scope"
//	returns com.google.common.collect.ImmutableMultimap<String, NameDef>
//	implemented by de.peeeq.wurstscript.attributes.Scopes.getDefinedNames

//WScope.attrVisibleNamesPrivate ""
//	returns com.google.common.collect.ImmutableMultimap<String, NameDef>
//	implemented by de.peeeq.wurstscript.attributes.Scopes.getVisibleNamesPrivate

//WScope.attrVisibleNamesProtected
//	returns com.google.common.collect.ImmutableMultimap<String, NameDef>
//	implemented by de.peeeq.wurstscript.attributes.Scopes.getVisibleNamesProtected

//WScope.attrVisibleNamesPublic
//	returns com.google.common.collect.ImmutableMultimap<String, NameDef>
//	implemented by de.peeeq.wurstscript.attributes.Scopes.getVisibleNamesPublic

WScope.attrNameLinks "returns a map of all the names visible in this scope."
	returns com.google.common.collect.ImmutableMultimap<String, de.peeeq.wurstscript.attributes.names.NameLink>
	implemented by de.peeeq.wurstscript.attributes.names.NameLinks.calculate

WScope.attrTypeNameLinks "returns a map of all the names visible in this scope."
	returns com.google.common.collect.ImmutableMultimap<String, de.peeeq.wurstscript.attributes.names.NameLink>
	implemented by de.peeeq.wurstscript.attributes.names.TypeNameLinks.calculate

WPackage.attrExportedNameLinks
	returns com.google.common.collect.ImmutableMultimap<String, de.peeeq.wurstscript.attributes.names.NameLink>
	implemented by de.peeeq.wurstscript.attributes.names.Exports.exportedNameLinks

WPackage.attrExportedTypeNameLinks
	returns com.google.common.collect.ImmutableMultimap<String, de.peeeq.wurstscript.attributes.names.NameLink>
	implemented by de.peeeq.wurstscript.attributes.names.Exports.exportedTypeNameLinks


AstElement.lookupType(String name, boolean showErrors)
	returns @Nullable TypeDef
	implemented by de.peeeq.wurstscript.attributes.names.NameResolution.lookupType

AstElement.lookupPackage(String name, boolean showErrors)
	returns @Nullable WPackage
	implemented by de.peeeq.wurstscript.attributes.names.NameResolution.lookupPackage

AstElement.lookupVar(String name, boolean showErrors)
	returns @Nullable NameDef
	implemented by de.peeeq.wurstscript.attributes.names.NameResolution.lookupVar

AstElement.lookupVarNoConfig(String name, boolean showErrors)
	returns @Nullable NameDef
	implemented by de.peeeq.wurstscript.attributes.names.NameResolution.lookupVarNoConfig

AstElement.lookupMemberVar(de.peeeq.wurstscript.types.WurstType receiverType, String name, boolean showErrors)
	returns @Nullable NameDef
	implemented by de.peeeq.wurstscript.attributes.names.NameResolution.lookupMemberVar

AstElement.lookupFuncs(String name, boolean showErrors)
	returns com.google.common.collect.ImmutableCollection<de.peeeq.wurstscript.attributes.names.NameLink>
	implemented by de.peeeq.wurstscript.attributes.names.NameResolution.lookupFuncs

AstElement.lookupFuncsNoConfig(String name, boolean showErrors)
	returns com.google.common.collect.ImmutableCollection<de.peeeq.wurstscript.attributes.names.NameLink>
	implemented by de.peeeq.wurstscript.attributes.names.NameResolution.lookupFuncsNoConfig

AstElement.lookupMemberFuncs(de.peeeq.wurstscript.types.WurstType receiverType, String name, boolean showErrors)
	returns com.google.common.collect.ImmutableCollection<de.peeeq.wurstscript.attributes.names.NameLink>
	implemented by de.peeeq.wurstscript.attributes.names.NameResolution.lookupMemberFuncs


AstElement.lookupType(String name)
	returns @Nullable TypeDef
	implemented by de.peeeq.wurstscript.attributes.names.NameResolution.lookupTypeShort

AstElement.lookupPackage(String name)
	returns @Nullable WPackage
	implemented by de.peeeq.wurstscript.attributes.names.NameResolution.lookupPackageShort

AstElement.lookupVar(String name)
	returns @Nullable NameDef
	implemented by de.peeeq.wurstscript.attributes.names.NameResolution.lookupVarShort

AstElement.lookupMemberVar(de.peeeq.wurstscript.types.WurstType receiverType, String name)
	returns @Nullable NameDef
	implemented by de.peeeq.wurstscript.attributes.names.NameResolution.lookupMemberVarShort

AstElement.lookupFuncs(String name)
	returns com.google.common.collect.ImmutableCollection<de.peeeq.wurstscript.attributes.names.NameLink>
	implemented by de.peeeq.wurstscript.attributes.names.NameResolution.lookupFuncsShort

AstElement.lookupMemberFuncs(de.peeeq.wurstscript.types.WurstType receiverType, String name)
	returns com.google.common.collect.ImmutableCollection<de.peeeq.wurstscript.attributes.names.NameLink>
	implemented by de.peeeq.wurstscript.attributes.names.NameResolution.lookupMemberFuncsShort
	
TypeParamDefs.attrTypes()
	returns com.google.common.collect.ImmutableList<de.peeeq.wurstscript.types.WurstType>
	implemented by de.peeeq.wurstscript.attributes.TypeParamTypes.calculte

Documentable.attrComment
	returns String
	implemented by de.peeeq.wurstscript.attributes.AttrWurstDoc.getComment
	
ExprOrStatements.attrUsedGlobalVariables
	returns com.google.common.collect.ImmutableList<VarDef>
	implemented by de.peeeq.wurstscript.attributes.UsedGlobalVariables.getUsedGlobals
	circular de.peeeq.wurstscript.utils.Utils.emptyList
	
FunctionDefinition.attrUsedGlobalVariables
	returns com.google.common.collect.ImmutableList<VarDef>
	implemented by de.peeeq.wurstscript.attributes.UsedGlobalVariables.getUsedGlobals
	circular de.peeeq.wurstscript.utils.Utils.emptyList
	
ExprOrStatements.attrReadGlobalVariables
	returns com.google.common.collect.ImmutableList<VarDef>
	implemented by de.peeeq.wurstscript.attributes.UsedGlobalVariables.getReadGlobals
	circular de.peeeq.wurstscript.utils.Utils.emptyList
	
FunctionDefinition.attrReadGlobalVariables
	returns com.google.common.collect.ImmutableList<VarDef>
	implemented by de.peeeq.wurstscript.attributes.UsedGlobalVariables.getReadGlobals
	circular de.peeeq.wurstscript.utils.Utils.emptyList

InitBlock.attrReadGlobalVariables
	returns com.google.common.collect.ImmutableList<VarDef>
	implemented by de.peeeq.wurstscript.attributes.UsedGlobalVariables.getReadGlobals

AstElement.attrUsedPackages
	returns com.google.common.collect.ImmutableCollection<WPackage>
	implemented by de.peeeq.wurstscript.attributes.UsedPackages.usedPackages
	
WPackage.attrImportedPackagesTransitive
	returns com.google.common.collect.ImmutableCollection<WPackage>
	implemented by de.peeeq.wurstscript.attributes.InitOrder.importedPackagesTrans
	
WPackage.attrInitDependencies
	returns com.google.common.collect.ImmutableList<WPackage>
	implemented by de.peeeq.wurstscript.attributes.InitOrder.initDependencies

WPackage.attrInitDependenciesTransitive
	returns com.google.common.collect.ImmutableList<WPackage>
	implemented by de.peeeq.wurstscript.attributes.InitOrder.initDependenciesTransitive
	

ExprIntVal.getValI
	returns int
	implemented by de.peeeq.wurstscript.attributes.IntVal.getValI

AstElement.description()
	returns String
	implemented by de.peeeq.wurstscript.attributes.Description.description

AstElement.descriptionHtml()
	returns String
	implemented by de.peeeq.wurstscript.attributes.DescriptionHtml.description
	
TypeExpr.isModuleUseTypeArg()
	returns boolean
	implemented by de.peeeq.wurstscript.attributes.SmallHelpers.isModuleUseTypeArg

AstElement.isSubtreeOf(AstElement other)
	returns boolean
	implemented by de.peeeq.wurstscript.attributes.SmallHelpers.isSubtreeOf
	
TypeParamDef.isStructureDefTypeParam()
	returns boolean
	implemented by de.peeeq.wurstscript.attributes.SmallHelpers.isStructureDefTypeParam

	
AstElement.prettyPrint(de.peeeq.wurstscript.attributes.prettyPrint.Spacer spacer, StringBuilder sb, int indent)
	returns void
	implemented by de.peeeq.wurstscript.attributes.prettyPrint.PrettyPrinter.prettyPrint


NameDef.getName()
    returns String
    implemented by de.peeeq.wurstscript.attributes.SmallHelpers.getName
    
WImport.getPackagename()
	returns String    
	implemented by de.peeeq.wurstscript.attributes.SmallHelpers.getPackagename
	
	
ExprNewObject.getTypeName()
	returns String    
	implemented by de.peeeq.wurstscript.attributes.SmallHelpers.getTypeName	

NameRef.getVarName()
	returns String    
	implemented by de.peeeq.wurstscript.attributes.SmallHelpers.getVarName

FuncRef.getFuncName()
	returns String    
	implemented by de.peeeq.wurstscript.attributes.SmallHelpers.getFuncName

	
	
ModuleUse.getModuleName()	
	returns String    
	implemented by de.peeeq.wurstscript.attributes.SmallHelpers.getModuleName
